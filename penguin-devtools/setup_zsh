#!/usr/bin/env zsh

# setup zsh

# source
src_url=${URL:-"https://git.linuxit.us/spider/zsh"}

# install to
dest_dir="$HOME/.zsh"

# plugins
plugin_lst="$dest_dir/plugins.txt"

# color output (msg,info,err,reset)
m='\033[1;35m'; i='\033[1;32m'; e='\033[1;31m'; n='\033[0m'

echo "${m}Installing zsh setup...${n}"
echo "${m}Source: ${i}$src_url${n}"
echo "${m}Target: ${i}$dest_dir${n}"
echo "${i}"
vared -cp "Confirm (y/n)? " ans
[[ "$ans" =~ ^[Yy]$ ]] || exit 1

# clone repo
if test -d $dest_dir
then
  echo "${m}Target '${i}$dest_dir${m}' already exists!${n}"
  exit 1
else
  if git clone $src_url $dest_dir
  then
    echo "${m}Cloned repo to ${i}$dest_dir${n}"
  else
    echo "${e}Failed to clone repo!${n}"
    exit 1
  fi
fi

# link .zshenv to $HOME
if test -f $HOME/.zshenv
then
  echo "${m}Target '${i}$HOME/.zshenv${m}' already exists!${n}"
else
  echo "${m}Linking to '${i}$dest_dir/.zshenv${m}'...${n}"
  ln -sf $dest_dir/.zshenv $HOME/.zshenv
fi

# get plugins
if test -f $plugin_lst
then
  mkdir -p $dest_dir/plugins &>/dev/null

  echo "${m}Reading plugin list...${n}"
  typeset -A plugins
  while read name url
  do
    plugins[$name]="$url"
  done < $plugin_lst
  unset name url

  echo "${m}Fetching plugins...${n}"
  for dest url in ${(kv)plugins}
  do
    echo "${m}Cloning '${i}$dest${m}'...${n}"
    dest="$dest_dir/plugins/$dest"
    if ! test -d $dest
    then
      git clone $url $dest
    fi
  done
  unset dest url
fi

echo "${i}Done${n}"
exit 0

# vim: ft=zsh ts=2 sw=2 et:
