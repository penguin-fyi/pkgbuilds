#!/usr/bin/env zsh

# setup tmux

# source
src_url=${URL:-"https://git.linuxit.us/spider/tmux"}

# install to
dest_dir="$HOME/.tmux"

# plugins
plugin_lst="$dest_dir/plugins.txt"

# color output (msg,info,err,reset)
m='\033[1;35m'; i='\033[1;32m'; e='\033[1;31m'; n='\033[0m'

echo "${m}Installing tmux setup...${n}"
echo "${m}Source: ${i}$src_url${n}"
echo "${m}Target: ${i}$dest_dir${n}"
echo "${i}"
vared -cp "Confirm (y/n)? " ans
[[ "$ans" =~ ^[Yy]$ ]] || exit 1

# clone repo
if test -d $dest_dir
then
  echo "${m}Target '${i}$dest_dir${m}' already exists!${n}"
  exit 1
else
  if git clone $src_url $dest_dir
  then
    echo "${m}Cloned repo to ${i}$dest_dir${n}"
  else
    echo "${e}Failed to clone repo!${n}"
    exit 1
  fi
fi

local conf="$HOME/.tmux.conf"
if test -L $conf
then
  echo "${m}Target '${i}$conf${m}' is symlink!${n}"
  echo "${m}Removing symlink!${n}"
  rm $conf
fi

if test -f $conf
then
  echo "${m}Target '${i}$conf${m}' already exists!${n}"
  echo "${m}Created backup '${i}$conf.bak${m}'!${n}"
  mv -b $conf $conf.backup
fi

local conf_new="$dest_dir/tmux.conf"
if test -f $conf_new
then
  echo "${m}Linking to '${i}$conf_new${m}' to '${i}$conf${m}'...${n}"
  ln -sf $conf_new $conf
fi
unset conf_new

# get plugins
if test -f $plugin_lst
then
  mkdir -p $dest_dir/plugins &>/dev/null

  echo "${m}Reading plugin list...${n}"
  typeset -A plugins
  while read name url
  do
    plugins[$name]="$url"
  done < $plugin_lst
  unset name url

  echo "${m}Fetching plugins...${n}"
  for dest url in ${(kv)plugins}
  do
    dest="$dest_dir/plugins/$dest"
    if ! test -d $dest
    then
      git clone $url $dest
    fi
  done
  unset dest url
fi

tmux source $conf
unset conf

echo "${m}Done${n}"
exit 0

# vim: ft=zsh ts=2 sw=2 et:
