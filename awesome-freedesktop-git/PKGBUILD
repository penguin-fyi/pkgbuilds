# Maintainer: Jeff M. Hubbard <jeffmhubbard@gmail.com>
_pkgname=awesome-freedesktop
pkgname=${_pkgname}-git
pkgver=r109.b77dee0
pkgrel=1
pkgdesc="Freedesktop.org compliant desktop entries and menu for Awesome WM"
arch=("any")
url="https://github.com/lcpz/$_pkgname"
license=("GPL2")
depends=("awesome")
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname")
sha256sums=('SKIP')
source=("${_pkgname}::git+${url}.git")

pkgver() {
    cd "${_pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    cd "$srcdir/${_pkgname}"
    install -dm755 "$pkgdir"/usr/share/lua/5.3/${_pkgname##*-}
    install -m644 {LICENSE,README.rst,*.lua} "$pkgdir"/usr/share/lua/5.3/${_pkgname##*-}
}
